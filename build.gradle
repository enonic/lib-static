plugins {
    id 'java'
    id 'jacoco'
    id 'maven-publish'
    id 'com.enonic.defaults' version '2.1.5'
    id 'com.enonic.xp.base' version '3.5.0'
    id 'com.github.node-gradle.node' version '7.1.0'
    id 'org.asciidoctor.jvm.convert' version '4.0.3'
    id "org.aim42.htmlSanityCheck" version '1.1.6'
}

sourceCompatibility = JavaVersion.VERSION_11
targetCompatibility = sourceCompatibility

def defaultEncoding = 'UTF-8'

compileJava {
  options.encoding = defaultEncoding
}

compileTestJava {
  options.encoding = defaultEncoding
}

dependencies {
   // compileOnly: This is used for dependencies that are only needed during
   // compilation. The library itself is not included in the final application
   // build.
  compileOnly "com.enonic.xp:core-api:${xpVersion}"
  compileOnly "com.enonic.xp:script-api:${xpVersion}"

  // implementation: This is the most common way to specify a dependency.
  // Libraries included with implementation are available during both compile
  // time and runtime. This means your code can use the functionality of the
  // dependency while compiling and when the final application runs.
  implementation "com.enonic.xp:lib-io:${xpVersion}"
  implementation "com.enonic.xp:lib-portal:${xpVersion}"
  // implementation "com.enonic.xp:lib-vhost:${xpVersion}"
  implementation 'com.enonic.lib:lib-router:3.1.0'

  testImplementation 'org.junit.jupiter:junit-jupiter-api:5.11.2'
  testImplementation 'org.junit.jupiter:junit-jupiter-engine:5.11.1'
  testImplementation 'org.junit.vintage:junit-vintage-engine:5.11.2'
  testImplementation "com.enonic.xp:testing:${xpVersion}"
  testImplementation "org.mockito:mockito-core:5.14.1"
}

processResources {
  exclude '**/.gitkeep'
  exclude '**/*.ts'
  exclude '**/tsconfig.json'
}

repositories {
    mavenCentral()
    xp.enonicRepo('dev')
}

node {
  // Whether to download and install a specific Node.js version or not
  // If false, it will use the globally installed Node.js
  // If true, it will download node using above parameters
  // Note that npm is bundled with Node.js
  download = true

  // Version of node to download and install (only used if download is true)
  // It will be unpacked in the workDir
  version = '20.14.0'
}

tasks.withType(Copy) {
  includeEmptyDirs = false
}

tasks.register('npmBuild', NpmTask) {
  args = [
    'run',
    '--silent',
    'build'
  ]
  dependsOn npmInstall
  dependsOn 'javadoc'
  environment = [
    'FORCE_COLOR': 'true',
    'LOG_LEVEL_FROM_GRADLE': gradle.startParameter.logLevel.toString(),
    'NODE_ENV': project.hasProperty('dev') || project.hasProperty('development') ? 'development' : 'production'
  ]
  inputs.dir 'src/main/resources'
  outputs.dir 'build/resources/main'
}
jar.dependsOn npmBuild

asciidoctor {
    sourceDir = file('docs')
    outputDir = file('build/docs')

    attributes 'source-highlighter': 'coderay'

    resources {
        from(sourceDir) {
            include '**/images/**'
            include '**/media/**'
        }
    }
}

htmlSanityCheck {
    dependsOn asciidoctor

    sourceDir = tasks.asciidoctor.outputDir
}

tasks.register('npmCheck', NpmTask) {
	dependsOn npmInstall
	args = [
		'run',
		'check'
	]
	environment = [
		'FORCE_COLOR': 'true',
	]
}

check.dependsOn npmCheck

tasks.register('npmTest', NpmTask) {
	args = [
		'run',
		'test'
	]
	dependsOn npmInstall
	environment = [
		'FORCE_COLOR': 'true',
	]
  inputs.dir 'src/jest'
  outputs.dir 'coverage'
}

test.dependsOn npmTest

compileTestJava.dependsOn(':npmBuild')

test {
  useJUnitPlatform()
}

jacocoTestReport {
  reports {
    xml.required = true
    html.required = true
  }
}

check.dependsOn jacocoTestReport
